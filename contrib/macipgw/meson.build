macipgw_deps = [bstring, iniparser]

if have_iconv
    macipgw_deps += iconv
endif

macipgw_sources = [
    'atp_input.c',
    'macip.c',
    'main.c',
    'nbp_lkup_async.c',
    'util.c',
]

if host_os == 'linux'
    macipgw_sources += 'tunnel_linux.c'
else
    macipgw_sources += 'tunnel_bsd.c'
endif

macipgw_includes = include_directories('../../libatalk/atp')

macipgw_c_args = [
    '-DSERVERTEXT="' + pkgconfdir + '/msg"',
    '-D_PATH_MACIPGWCONF="' + pkgconfdir + '/macipgw.conf"',
    dversion,
]

executable(
    'macipgw',
    macipgw_sources,
    include_directories: [root_includes, macipgw_includes],
    link_with: libatalk,
    dependencies: macipgw_deps,
    c_args: macipgw_c_args,
    export_dynamic: true,
    install: true,
    install_dir: sbindir,
    build_rpath: rpath_libdir,
    install_rpath: rpath_libdir,
)
